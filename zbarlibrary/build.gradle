apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }


    def isBuildSo = false
    if (isBuildSo) { // 构建生成 so 文件
        sourceSets {
            main {
                jniLibs.srcDirs = []
            }
        }
        externalNativeBuild {
            ndkBuild {
                path 'src/main/jni/Android.mk'
            }
        }
    } else { // 生成 so 文件后，打包时直接使用生成的 so
        sourceSets {
            main {
                jni.srcDirs = []
            }
        }
        delete(file('.externalNativeBuild'), file('src/main/libs'), file('src/main/obj'))
    }
}

dependencies {
    if (hasProperty("publishAar")) {
        api "cn.bingoogolapple:bga-qrcode-core:1.3.7"
    }
}

// ./gradlew :zbar:clean :zbar:build :zbar:bintrayUpload -PpublishAar
if (hasProperty("publishAar")) {
    apply from: 'https://raw.githubusercontent.com/bingoogolapple/PublishAar/master/jcenter-release.gradle'
}

tasks.whenTaskAdded {
    if (it.name == 'releaseAndroidJavadocs') {
        it.enabled = false
    }
}



version = "0.0.1"

def siteUrl = 'https://github.com/JackiePenghe/QrCodeDemo'
def gitUrl = 'https://github.com/JackiePenghe/QrCodeDemo.git'

group = "com.sscl"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'QRcodeLibrary For Android'
                description 'A QR code library for Android.'
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'jackiepenghe'
                        name 'jackiepenghe'
                        email 'p15583101939@gmail.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "zxinglibrary"
        userOrg = 'sscl'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
